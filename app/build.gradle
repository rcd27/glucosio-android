/*
 * Copyright (C) 2016 Glucosio Foundation
 *
 * This file is part of Glucosio.
 *
 * Glucosio is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3.
 *
 * Glucosio is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Glucosio.  If not, see <http://www.gnu.org/licenses/>.
 *
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion buildToolsVer

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 41
        versionName '1.4.0'
        applicationId 'org.glucosio.android'

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true

            versionNameSuffix '-DEVEL'
            applicationIdSuffix '.daily'

            testCoverageEnabled true

            buildConfigField 'String', 'GOOGLE_ANALYTICS_TRACKER', '"UA-68882401-2"'
            buildConfigField "String[]", "TRANSLATION_ARRAY", "new String[]{}"
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            buildConfigField 'String', 'GOOGLE_ANALYTICS_TRACKER', '"UA-68882401-2"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String[]", "TRANSLATION_ARRAY", getAllLocales()
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
            // configure the test JVM arguments
            jvmArgs '-noverify'
        }

        unitTests {
            includeAndroidResources = true
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    wearApp project(':wear')

    // Android Support libraries
    implementation 'com.google.android.support:wearable:2.0.0'

    implementation "com.android.support:appcompat-v7:${androidSupportVer}"
    implementation "com.android.support:design:${androidSupportVer}"
    implementation "com.android.support:cardview-v7:${androidSupportVer}"
    implementation "com.android.support:recyclerview-v7:${androidSupportVer}"
    implementation "com.android.support:percent:${androidSupportVer}"

    // Google Play Services
    implementation "com.google.android.gms:play-services-analytics:${googlePlayServicesVer}"
    implementation "com.google.android.gms:play-services-drive:${googlePlayServicesVer}"
    implementation "com.google.android.gms:play-services-wearable:${googlePlayServicesVer}"
    implementation "com.google.android.gms:play-services-gcm:${googlePlayServicesVer}"

    // Firebase
    implementation "com.google.firebase:firebase-core:${googlePlayServicesVer}"
    implementation "com.google.firebase:firebase-crash:${googlePlayServicesVer}"
    implementation "com.google.firebase:firebase-invites:${googlePlayServicesVer}"
    implementation "com.google.firebase:firebase-messaging:${googlePlayServicesVer}"

    // Other libraries
    implementation 'com.github.paolorotolo:expandableheightlistview:1.0.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.wdullaer:materialdatetimepicker:2.4.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    implementation 'uk.co.chrisjenx:calligraphy:2.2.0'
    implementation 'net.danlew:android.joda:2.9.5.1'
    implementation('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }

    // Butterknife
    implementation "com.jakewharton:butterknife:${butterKnifeVer}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVer}"

    testImplementation "junit:junit:${junitVer}"
    testImplementation "org.mockito:mockito-all:${mockitoVer}"
    testImplementation "com.squareup.assertj:assertj-android:${androidAssertJVer}"
    testImplementation("org.robolectric:robolectric:3.5.1") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation("org.robolectric:shadows-support-v4:3.3.2") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation 'io.reactivex:rxjava:1.3.4'

    testImplementation "org.powermock:powermock-module-junit4:1.6.4"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.6.4"
    testImplementation "org.powermock:powermock-api-mockito:1.6.4"
    testImplementation "org.powermock:powermock-classloading-xstream:1.6.4"

    // Testing libraries
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation "com.android.support:support-annotations:${androidSupportVer}"
    androidTestImplementation "com.android.support:recyclerview-v7:${androidSupportVer}"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2'
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:${androidSupportVer}"
    resolutionStrategy.force "com.android.support:design:${androidSupportVer}"
    resolutionStrategy.force "com.android.support:appcompat-v7:${androidSupportVer}"
}

def getAllLocales() {
    def foundLocales = new StringBuilder()
    foundLocales.append("new String[]{")

    fileTree("src/main/res").visit { FileVisitDetails details ->
        if (details.file.path.endsWith("strings.xml")) {
            def languageCode = details.file.parent.tokenize('/\\').last().replaceAll('values-', '').replaceAll('-r', '-')
            languageCode = (languageCode == "values") ? "en" : languageCode
            foundLocales.append("\"").append(languageCode).append("\"").append(",")
        }
    }

    foundLocales.append("}")
    //Don't forget to remove the trailing comma
    return foundLocales.toString().replaceAll(',}', '}')
}

apply plugin: 'com.google.gms.google-services'

def coverageSourceDirs = ['src/main/java']

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = 'Reporting'

    description = 'Generate Jacoco coverage reports'

    classDirectories = fileTree(dir: 'build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewBinder*.*',
                       '**/*$InjectAdapter.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',])

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testDebugUnitTest.exec')

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    sourceDirs = files(['src/main/java']).files.absolutePath
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
}

// run: gradlew -PwithDexcount assembleDebug
def dexCount = project.hasProperty('withDexcount')
if (dexCount) {
    apply plugin: 'com.getkeepsafe.dexcount'

    dexcount {
        includeTotalMethodCount = true
    }
}
